import org.gradle.internal.os.OperatingSystem

apply plugin: 'maven-publish'
apply plugin: 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin'

def ntcoreFile = file("$buildDir/ntcore.txt")

task outputVersions() {
    description = 'Prints the version of ntcore to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(ntcoreFile)

    doFirst {
        buildDir.mkdir()
    }

    doLast {
        ntcoreFile.write WPILibVersion.version
    }
}

task clean(type: Delete) {
    delete ntcoreFile
}

outputVersions.mustRunAfter clean

project(':native').build.dependsOn outputVersions
if (project.buildArm) {
    project(':arm').build.dependsOn outputVersions
}

// We change what repo we publish to depending on whether this is a development, beta, stable, or full
// release. This is set up in the main gradle file.
publishing {
    publications {
        def nat = project('native')
        if (!project.hasProperty('skipJava')) {
            java(MavenPublication) {
                artifact nat.jar
                artifact nat.networktablesJavaSource
                artifact nat.networktablesJavadoc
                if (project.buildArm) {
                    def natArm = project(':arm')
                    artifact natArm.jar
                    // If the library is not embedded include it in the repo
                    if (!project.hasProperty('compilerPrefix')) {
                        artifact natArm.ntcoreZip
                    }
                }

                if (project.hasProperty('makeDesktop')) {
                    artifact nat.jar, {
                        classifier = 'desktop'
                    }
                }

                groupId 'edu.wpi.first.wpilib.networktables.java'
                artifactId 'NetworkTables'
                version WPILibVersion.version
            }
        }
        cpp(MavenPublication) {
            artifact nat.ntcoreZip
            artifact ntcoreSourceZip
            if (project.buildArm) {
                artifact project(':arm').ntcoreZip
            }

            if (project.hasProperty('makeDesktop')) {
                artifact nat.ntcoreZip, {
                    classifier = 'desktop'
                }
            }

            groupId 'edu.wpi.first.wpilib.networktables.cpp'
            artifactId 'NetworkTables'
            version WPILibVersion.version
        }
    }
}
